--- RCPTT testcase ---
Format-Version: 1.0
Element-Name: Execution
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _t24QgB4NEeWDyJhARpwrcA
Runtime-Version: 2.0.0.201509132315
Save-Time: 9/30/15 9:45 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content


proc "check-debug-state" [val expected] {
	get-window "Debug Configurations" | get-button Debug | get-property enablement | equals $expected | verify-true
}

mx-new-xuml-project "TestProject"
mx-copy-model-to-test "HelloWorld"

mx-make-new-xuml-run-config


// Check that the Debug button is disabled
get-window "Debug Configurations" | get-button Debug | get-property enablement | equals false | verify-true


// -------------------------------------------------
// Make new xUML project

mx-new-xuml-project-select-model "TestProject/HelloWorld.uml"
check-debug-state false

mx-new-xuml-project-select-class "<Class> HelloWorld"
check-debug-state false

mx-new-xuml-project-select-main-function "<Operation> run \\(\\)"
check-debug-state true

mx-new-xuml-project-save-configuration

// -------------------------------------------------
// Execute run configuration

mx-execute-run-configuration

// Execution log must not contain errors
get-view Console | get-text-viewer | get-property "getText()" | contains "logError" | verify-false 


// -------------------------------------------------
// Execute debug configuration, checking breakpoints

mx-execute-debug-configuration

// The Model Explorer view is placed in an unusual location...
get-tab-folder -index 6 | get-tab-item "Model Explorer" | click

get-tree -index 2 | select "model/HelloWorldComponent/HelloWorld/HelloWorld/HelloWorldBehavior/Region1/Hello" 
    | get-menu "Moka/Debug/Toggle breakpoint" | click



// Check if it appears in the Breakpoints view
get-view Breakpoints | get-tree | get-item "model::HelloWorldComponent::HelloWorld::HelloWorld::HelloWorldBehavior::Region1::Hello" 
    | get-property caption | equals "model::HelloWorldComponent::HelloWorld::HelloWorld::HelloWorldBehavior::Region1::Hello" | verify-true

mx-execute-debug-configuration

// Check for pause on breakpoint
get-view Debug | get-tree 
    | get-item "xUMLTestRunConfig \\[xUML-RT Executor\\]/model::HelloWorldComponent::HelloWorld::HelloWorld::run/Default Component/HelloWorld/HelloWorld#0/Hello \\[State\\]" 
    | get-property caption | equals "Hello [State]" | verify-true



global [val fillColorHighlightedNode 5487098]

// Manually select state
get-view Debug | get-tree 
    | select "xUMLTestRunConfig [xUML-RT Executor]/model::HelloWorldComponent::HelloWorld::HelloWorld::run/Default Component/HelloWorld/HelloWorld#0/Hello [State]"

// Check for breakpoint highlighting
get-editor "HelloWorld.di" | get-diagram -index 1 | get-edit-part -name HelloWorldBehavior | get-edit-part 
    -name HelloWorldBehavior | get-edit-part -className CustomStateMachineCompartmentEditPart | get-edit-part 
    -name Region1 | get-edit-part -name Region1 | get-edit-part -name Hello | get-property fillColor | equals $fillColorHighlightedNode
    | verify-true


// Go around the first time
get-menu "Run/Resume" | click

// Check for pause on breakpoint
get-view Debug | get-tree 
    | get-item "xUMLTestRunConfig \\[xUML-RT Executor\\]/model::HelloWorldComponent::HelloWorld::HelloWorld::run/Default Component/HelloWorld/HelloWorld#0/Hello \\[State\\]" 
    | get-property caption | equals "Hello [State]" | verify-true

// Check for breakpoint highlighting
get-editor "HelloWorld.di" | get-diagram -index 1 | get-edit-part -name HelloWorldBehavior | get-edit-part 
    -name HelloWorldBehavior | get-edit-part -className CustomStateMachineCompartmentEditPart | get-edit-part 
    -name Region1 | get-edit-part -name Region1 | get-edit-part -name Hello | get-property fillColor | equals $fillColorHighlightedNode 
    | verify-true

// Go to exit the second time
get-menu "Run/Resume" | click

// Check for termination
get-view Debug | get-tree | get-item "<terminated>xUMLTestRunConfig \\[xUML-RT Executor\\]" | get-property caption 
    | equals "<terminated>xUMLTestRunConfig [xUML-RT Executor]" | verify-true

// Highlighting should be turned off on breakpoint as execution is terminated
with [get-editor "HelloWorld.di" | get-diagram -index 1 | get-edit-part -name HelloWorldBehavior 
    | get-edit-part -name HelloWorldBehavior | get-edit-part -className CustomStateMachineCompartmentEditPart 
    | get-edit-part -name Region1 | get-edit-part -name Region1 | get-edit-part -name Hello] {
    get-property "modelPropertyNodes[20].name" | equals fillColor | verify-true
    get-property "modelPropertyNodes[20].value" | equals $fillColorHighlightedNode | verify-false
}



// Switching back to Papyrus perspective so that the final Problems view check can execute
mx-open-perspective "Papyrus"

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
