package hu.eltesoft.modelexecution.uml.incquery

import "http://www.eclipse.org/uml2/5.0.0/UML"

pattern
Region(region : Region, regionName, containerClassName, initPseudostate : Pseudostate) {
	Region.name(region, regionName);

	Class.name(containerClass, containerClassName);
	Class.ownedBehavior(containerClass, stateMachine);
	StateMachine.region(stateMachine, region);

	Pseudostate.container(initPseudostate, region);
	Pseudostate.kind(initPseudostate, ::initial);
}

pattern InitialTransition(region : Region, initPseudostate : Pseudostate, initTransition : Transition, initTransitionName, targetName) {
	Pseudostate.container(initPseudostate, region);
	Pseudostate.kind(initPseudostate, ::initial);

	Transition.source(initTransition, initPseudostate);
	Transition.name(initTransition, initTransitionName);
	
	Transition.target(initTransition, targetState);
	State.name(targetState, targetName);
}

pattern State(region : Region, state : State, stateName) {
	State.container(state, region);
	State.name(state, stateName);
}

pattern Entry(region : Region, state : State, entryName) {
	State.container(state, region);
	State.entry(state, entry);
	Behavior.name(entry, entryName);
}

pattern Exit(region : Region, state : State, exitName) {
	State.container(state, region);
	State.exit(state, exit);
	Behavior.name(exit, exitName);
}

pattern
Transition(region : Region, sourceState : State, transition : Transition, transitionName, eventName, targetName) {
	Transition.container(transition, region);

	Transition.source(transition, sourceState);
	Transition.target(transition, targetState);
	State.name(targetState, targetName);

	Transition.name(transition, transitionName);

	Transition.trigger(transition, trigger);
	Trigger.event(trigger, event);
	Event.name(event, eventName);
}

pattern
TransitionEffect(region : Region, transition : Transition, effectName) {
	Transition.container(transition, region);
	Transition.effect(transition, effect);
	Behavior.name(effect, effectName);
}



