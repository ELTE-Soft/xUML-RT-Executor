package hu.eltesoft.modelexecution.validation

import "platform:/resource/org.eclipse.uml2.uml/model/UML.ecore"

import hu.eltesoft.modelexecution.uml.incquery.Cls
import hu.eltesoft.modelexecution.uml.incquery.Event
import hu.eltesoft.modelexecution.uml.incquery.Association
import hu.eltesoft.modelexecution.uml.incquery.AssociationClass
import hu.eltesoft.modelexecution.uml.incquery.NumericValue
import hu.eltesoft.modelexecution.uml.incquery.SignalEvent

private pattern GeneralPackagedElement(elem : Element) {
	find Package(elem);
} or {
	find Component(elem);
} or {
	find Event(elem);
} or {
	find Signal(elem);
} or {
	find Cls(elem);
} or {
	find Association(elem);
} or {
	find AssociationClass(elem);
}

//////////////////////////
// model
/////////////////////////
private pattern Model(md : Model) {
	Model(md);
}

@Violation(message = "Models can only contain components, packages, events, signals, classes, associations, package imports and profile application", mark = { "elem" })
pattern ForbiddenModelMember(md : Model, elem : Element) {
	Package.ownedElement(md, elem);
	neg find GeneralPackagedElement(elem);
	neg find PackageImport(elem);
	neg find ProfileApplication(elem);
}

@Violation(message = "Model viewpoints are not supported", mark = { "md" })
pattern ModelViewpoint(md : Model) {
	Model.viewpoint(md, _);
}

//////////////////////////
// package imports
/////////////////////////
private pattern PackageImport(pi : PackageImport) {
	PackageImport(pi);
}

@Violation(message = "Package import must reference the package to be imported", mark = { "pi" })
pattern PackageImportWithoutImportedPackage(pi : PackageImport) {
	neg find PackageImportPackage(pi, _);
}

private pattern PackageImportPackage(pi : PackageImport, imported : Package) {
	PackageImport.importedPackage(pi, imported);
}

@Violation(message = "Package imports must be public", mark = { "pi" })
pattern PackageImportNonPublic(pi : PackageImport) {
	PackageImport.visibility(pi, visibility);
	visibility != VisibilityKind::public;
}

///////////////////////
// packages
///////////////////////
private pattern Package(pk : Package) {
	Package(pk);
	neg find Model(pk);
}

@Violation(message = "Package merge is not supported", mark = { "pk" })
pattern PackageMerge(pk : Package) {
	Package.packageMerge(pk, _);
}

@Violation(message = "Template binding is not supported for packages", mark = { "pk" })
pattern PackageTemplateBinding(pk : Package) {
	Package.templateBinding(pk, _);
}

@Violation(message = "Template parameters are not supported for packages", mark = { "pk" })
pattern PackageTemplateParameter(pk : Package) {
	Package.templateParameter(pk, _);
}

@Violation(message = "Packages must be public", mark = { "pk" })
pattern PackageNonPublic(pk : Package) {
	Package.visibility(pk, visibility);
	visibility != VisibilityKind::public;
}

@Violation(message = "Packages can only contain components, packages, events, signals, associations and classes", mark = { "elem" })
pattern ForbiddenPackageMember(pk : Package, elem : Element) {
	find Package(pk);
	Package.ownedElement(pk, elem);
	neg find GeneralPackagedElement(elem);
}

///////////////////////
// components
///////////////////////
private pattern Component(cp : Component) {
	Component(cp);
}

@Violation(message = "Packages can only contain components, packages, events, signals and classes", mark = { "elem" })
pattern ForbiddenComponentMember(cp : Component, elem : Element) {
	Component.ownedElement(cp, elem);
	neg find GeneralPackagedElement(elem);
}

@Violation(message = "Components must be public", mark = { "cp" })
pattern ComponentNonPublic(cp : Component) {
	Component.visibility(cp, visibility);
	visibility != VisibilityKind::public;
}

@Violation(message = "Component classifier behaviors are not supported", mark = { "cp" })
pattern ComponentClassifierBehavior(cp : Component) {
	Component.classifierBehavior(cp, _);
}

@Violation(message = "Directly instantiated components are not supported", mark = { "cp" })
pattern ComponentDirectlyInstantiated(cp : Component) {
	Component.isIndirectlyInstantiated(cp, false);
}


///////////////////////
// events
///////////////////////
private pattern IsSignalEvent(ev : SignalEvent) {
	SignalEvent(ev);
}

@Violation(message = "Only signal events are supported", mark = { "ev" })
pattern EventIsNotSignalEvent(ev : Event) {
	neg find IsSignalEvent(ev);
}

@Violation(message = "Signal event must have an associated signal", mark = { "ev" })
pattern SignalEventWithNoEvent(ev : SignalEvent) {
	neg find SignalEvent(ev, _);
}

@Violation(message = "The signal {sg} cannot appear as signal of two signal events", mark = { "ev1", "ev2" })
pattern SignalInMultipleSignalEvents(sg : Signal, ev1 : SignalEvent, ev2 : SignalEvent) {
	SignalEvent.signal(ev1, sg);
	SignalEvent.signal(ev2, sg);
	ev1 != ev2;
}

@Violation(message = "Event visibility must be public", mark = { "ev" })
pattern EventNonPublic(ev : Event) {
	Event.visibility(ev, visibility);
	visibility != VisibilityKind::public;
}


//////////////////////////////
// signals
//////////////////////////////
private pattern Signal(sg : Signal) {
	Signal(sg);
}

@Violation(message = "Signals cannot be abstract", mark = { "sg" })
pattern AbstractSignal(sg : Signal) {
	Signal.isAbstract(sg, true);
}

@Violation(message = "Signals properties cannot be static", mark = { "prop" })
pattern SignalPropertyStatic(sg : Signal, prop : Property) {
	Signal.attribute(sg, prop);
	Property.isStatic(prop, true);
}

@Violation(message = "Signals properties must be single", mark = { "prop" })
pattern SignalPropertyMultiple(sg : Signal, prop : Property) {
	Signal.attribute(sg, prop);
	Property.upperValue(prop, high);
	find NumericValue(high, hv);
	check (hv as Integer > 1);
}

@Violation(message = "Signals properties must be primitive types", mark = { "prop" })
pattern SignalPropertyNotPrimitive(sg : Signal, prop : Property) {
	Signal.attribute(sg, prop);
	Property.type(prop, ty);
	neg find PrimitiveType(ty);
}

private pattern PrimitiveType(ty : PrimitiveType) {
	PrimitiveType(ty);
}

@Violation(message = "Signal visibility must be public", mark = { "sg" })
pattern SignalNonPublic(sg : Signal) {
	Signal.visibility(sg, visibility);
	visibility != VisibilityKind::public;
}

@Violation(message = "Signals can only own their properties", mark = { "elem" })
pattern SignalElement(elem : Element) {
	Signal.ownedElement(sg, elem);
	neg find SignalProperty(sg, elem);
}

private pattern SignalProperty(sg : Signal, prop : Property) {
	Signal.attribute(sg, prop);
}

///////////////////////
// profile application
///////////////////////
private pattern ProfileApplication(pa : ProfileApplication) {
	ProfileApplication(pa);
}


